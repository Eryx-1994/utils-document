### 本地文件

```
// 1、相对路径
File configFile = file('src/config.xml')
configFile.createNewFile();
// 2、绝对路径
configFile = file('D:\\config.xml')
configFile.createNewFile()
// 3、文件对象
configFile = new File('src/config2.xml')
configFile.createNewFile()
```

### 文件集合

```
def fileCollection = files('src/test1.txt', new File('src/test2.txt'), ['src/test3.txt', 'src/test4.txt'])
fileCollection.forEach() { File item ->
    {
        // 创建文件
        item.createNewFile()
        // 输出文件名
        println item.name
    }
}

println "*******************************"

// 集合转换为Set
Set set1 = fileCollection.files
Set set2 = fileCollection as Set
for (item in set2) {
    println "set2: ${item.name}"
}

println "*******************************"

// 转换为List列表
List list = fileCollection as List
for (item in list) {
    println "list: ${item.name}"
}
println "*******************************"
// 添加或者删除一个集合
def union = fileCollection + files('src/test5.txt')
union.forEach() { File item ->
    println "union: ${item.name}"
}

println "*******************************"

def minus = fileCollection - files('src/test3.txt')
minus.forEach() { File item ->
    println "minus: ${item.name}"
}
```

### 文件树

```
// 1、使用路径创建文件树对象，同时指定包含的文件
ConfigurableFileTree tree = fileTree('src/main').include('**/*.java')
// 2、通过闭包创建文件树:
tree = fileTree('src/main') {
    include '**/*.java'
}
// 3、通过路径和闭包创建文件树：具名参数给map传值
tree = fileTree(dir: 'src/main', includes: ['**/*.java', '**/*.xml', '**/*.txt'], exclude: '**/*test*/**')
tree = fileTree(dir: 'src/main', include: '**/*.java')
tree.forEach() { File item ->
    println "****** ${item.name}"
}
```

### 文件拷贝

方式一：Copy任务

```
task copyTask(type: Copy) {
    // 拷贝指定目录下的文件
    from 'src/main/'
    // 包含
    include('**/*.*')
    // 排除
//    exclude('**/*.txt')

    // 从Zip压缩文件中拷贝内容
//    from zipTree('src/main/test.zip')

    // 重命名文件
    rename { String fileName -> fileName.replace('test1.txt', 't11.txt') }

    // 拷贝到的目标目录
    into 'build/temp'
}
```

方式二：copy方法

```
copy {
    // 相对路径
    from file('src/main/test.txt')
    into this.buildDir.absolutePath
}
```

### 归档文件

运行之后会生成jar包在`build/libs`目录下

```
task myJar(type: Jar) {
    from 'src/main'
    into 'build'
    baseName = 'myApp'
}
```